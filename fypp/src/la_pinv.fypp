#:include "common.fypp"
! Compute the (Moore-Penrose) pseudo-inverse of a matrix.
module la_pseudoinverse
     use la_constants
     use la_blas
     use la_lapack
     use la_state_type
     use la_svd, only: svd
     use iso_fortran_env,only:real32,real64,real128,int8,int16,int32,int64,stderr => error_unit
     implicit none(type,external)
     private

     !> @brief Compute the pseudo-inverse of a matrix.
     !!
     !! This function computes the Moore-Penrose pseudo-inverse of a real or complex matrix \f$ A \f$.
     !! The pseudo-inverse is computed using Singular Value Decomposition (SVD):
     !!
     !! \f$ A^+ = V \Sigma^+ U^T \f$
     !!
     !! where \f$ U \f$ and \f$ V \f$ are unitary matrices, and \f$ \Sigma^+ \f$ is the 
     !! pseudo-inverse of the singular values.
     !!
     !! @param[in] A The input matrix of size \f$ [m, n] \f$.
     !! @param[in] rtol (Optional) Relative tolerance for singular value truncation. If not provided, 
     !!                 a default value is used.
     !! @param[out] err (Optional) A state return flag. If an error occurs and `err` is not provided, 
     !!                 the function will stop execution.
     !!
     !! @return The pseudo-inverse matrix \f$ A^+ \f$ of size \f$ [n, m] \f$.
     !!
     !! @note This function relies on LAPACK's SVD routines (`*GESVD` or `*GESDD`).
     !! @warning If `rtol` is too large, important singular values may be discarded, 
     !!          leading to inaccurate results.
     !!
     public :: pinv

     !> @brief Compute the pseudo-inverse of a matrix (subroutine version).
     !!
     !! This subroutine computes the Moore-Penrose pseudo-inverse of a real or complex matrix \f$ A \f$,
     !! storing the result in a pre-allocated output matrix \f$ A^+ \f$.
     !! The computation is based on Singular Value Decomposition (SVD):
     !!
     !! \f$ A^+ = V \Sigma^+ U^T \f$
     !!
     !! where \f$ U \f$ and \f$ V \f$ are unitary matrices, and \f$ \Sigma^+ \f$ is the 
     !! pseudo-inverse of the singular values.
     !!
     !! @param[in,out] A The input matrix of size \f$ [m, n] \f$. Its contents may be modified.
     !! @param[out] pinva The output pseudo-inverse matrix of size \f$ [n, m] \f$.
     !! @param[in] rtol (Optional) Relative tolerance for singular value truncation.
     !! @param[out] err (Optional) A state return flag. If an error occurs and `err` is not provided, 
     !!                 the function will stop execution.
     !!
     !! @note This subroutine is useful when the output matrix `pinva` is already allocated and avoids
     !!       memory allocation inside the routine.
     !! @warning The input matrix `A` may be modified during computation.
     !!
     public :: pseudoinvert

     !> @brief Compute the pseudo-inverse of a matrix using the `.pinv.` operator.
     !!
     !! This operator computes the Moore-Penrose pseudo-inverse of a real or complex matrix \f$ A \f$
     !! using Singular Value Decomposition (SVD):
     !!
     !! \f$ A^+ = V \Sigma^+ U^T \f$
     !!
     !! where \f$ U \f$ and \f$ V \f$ are unitary matrices, and \f$ \Sigma^+ \f$ is the 
     !! pseudo-inverse of the singular values.
     !!
     !! @param[in] A The input matrix of size \f$ [m, n] \f$.
     !!
     !! @return The pseudo-inverse matrix \f$ A^+ \f$ of size \f$ [n, m] \f$.
     !!
     !! @note This operator is a shorthand for calling `pinv(A)`, allowing expressions such as:
     !!       \f$ X = .pinv.A \f$
     !! @warning The accuracy of the pseudo-inverse depends on the condition number of \f$ A \f$.
     !!
     public :: operator(.pinv.)

     !> @brief Compute the pseudo-inverse of a matrix.
     !!
     !! This function computes the Moore-Penrose pseudo-inverse of a real or complex matrix \f$ A \f$.
     !! The pseudo-inverse is computed using Singular Value Decomposition (SVD):
     !!
     !! \f$ A^+ = V \Sigma^+ U^T \f$
     !!
     !! where \f$ U \f$ and \f$ V \f$ are unitary matrices, and \f$ \Sigma^+ \f$ is the 
     !! pseudo-inverse of the singular values.
     !!
     !! @param[in] A The input matrix of size \f$ [m, n] \f$.
     !! @param[in] rtol (Optional) Relative tolerance for singular value truncation. If not provided, 
     !!                 a default value is used.
     !! @param[out] err (Optional) A state return flag. If an error occurs and `err` is not provided, 
     !!                 the function will stop execution.
     !!
     !! @return The pseudo-inverse matrix \f$ A^+ \f$ of size \f$ [n, m] \f$.
     !!
     !! @note This function relies on LAPACK's SVD routines (`*GESVD` or `*GESDD`).
     !! @warning If `rtol` is too large, important singular values may be discarded, 
     !!          leading to inaccurate results.
     !!
     interface pinv
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure la_pseudoinverse_${ri}$
        #:endfor
     end interface pinv

     interface pseudoinvert
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure la_pseudoinvert_${ri}$
        #:endfor
     end interface pseudoinvert

     interface operator(.pinv.)
        #:for rk,rt,ri in ALL_KINDS_TYPES
        module procedure la_pinv_${ri}$_operator
        #:endfor
     end interface operator(.pinv.)
     
     character(*), parameter :: this = 'pseudo-inverse'

     contains

     #:for rk,rt,ri in ALL_KINDS_TYPES

     ! Compute the in-place pseudo-inverse of matrix a
     subroutine la_pseudoinvert_${ri}$(a,pinva,rtol,err)
         !> Input matrix a[m,n]
         ${rt}$, intent(inout) :: a(:,:)
         !> Output pseudo-inverse matrix
         ${rt}$, intent(inout) :: pinva(:,:)
         !> [optional] ....
         real(${rk}$), optional, intent(in) :: rtol
         !> [optional] state return flag. On error if not requested, the code will stop
         type(la_state), optional, intent(out) :: err

         ! Local variables
         real(${rk}$) :: tolerance,cutoff
         real(${rk}$), allocatable :: s(:)
         ${rt}$, allocatable :: u(:,:),vt(:,:)
         type(la_state) :: err0
         integer(ilp) :: m,n,k,i,j
         
         ! Problem size
         m = size(a,1,kind=ilp)
         n = size(a,2,kind=ilp)
         k = min(m,n)
         if (m<1 .or. n<1) then
            err0 = la_state(this,LINALG_VALUE_ERROR,'invalid matrix size: a=',[m,n])
            call err0%handle(err)
            return
         end if         
         
         if (any(shape(pinva,kind=ilp)/=[n,m])) then
            err0 = la_state(this,LINALG_VALUE_ERROR,'invalid pinv size:',shape(pinva),'should be',[n,m])
            call err0%handle(err)
            return
         end if                  
         
         ! Singular value threshold
         tolerance = max(m,n)*epsilon(0.0_${rk}$)
         
         ! User threshold: fallback to default if <=0
         if (present(rtol)) then 
            if (rtol>0.0_${rk}$) tolerance = rtol                
         end if
         
         allocate(s(k),u(m,k),vt(k,n))
         call svd(a,s,u,vt,overwrite_a=.false.,full_matrices=.false.,err=err0)
         if (err0%error()) then 
            err0 = la_state(this,LINALG_ERROR,'svd failure -',err0%message)
            call err0%handle(err)
            return
         endif
         
         !> Discard singular values
         cutoff = tolerance*maxval(s)
         s = merge(1/s,0.0_${rk}$,s>cutoff)

         ! Get pseudo-inverse: A_pinv = V * (diag(1/s) * U^H) = V * (U * diag(1/s))^H
         
         ! 1) compute (U * diag(1/s)) in-place
         forall (i=1:m,j=1:k) u(i,j) = s(j)*u(i,j)
            
         ! 2) commutate matmul: A_pinv = V^H * (U * diag(1/s))^H = ((U * diag(1/s)) * V^H)^H. 
         !    This avoids one matrix transpose
         #:if rt.startswith('complex')
         pinva = conjg(transpose(matmul(u,vt)))
         #:else
         pinva = transpose(matmul(u,vt))
         #:endif

     end subroutine la_pseudoinvert_${ri}$

     ! Function interface
     function la_pseudoinverse_${ri}$(a,rtol,err) result(pinva)
         !> Input matrix a[m,n]
         ${rt}$, intent(in), target :: a(:,:)
         !> [optional] ....
         real(${rk}$), optional, intent(in) :: rtol         
         !> [optional] state return flag. On error if not requested, the code will stop
         type(la_state), optional, intent(out) :: err
         !> Matrix pseudo-inverse
         ${rt}$ :: pinva(size(a,2,kind=ilp),size(a,1,kind=ilp))         
         
         ! Use pointer to circumvent svd intent(inout) restriction
         ${rt}$, pointer :: ap(:,:)
         ap => a
         
         call la_pseudoinvert_${ri}$(ap,pinva,rtol,err)

     end function la_pseudoinverse_${ri}$

     ! Inverse matrix operator
     function la_pinv_${ri}$_operator(a) result(pinva)
         !> Input matrix a[m,n]
         ${rt}$, intent(in), target :: a(:,:)
         !> Result matrix
         ${rt}$ :: pinva(size(a,2,kind=ilp),size(a,1,kind=ilp))

         ! Use pointer to circumvent svd intent(inout) restriction
         ${rt}$, pointer :: ap(:,:)
         ap => a

         call la_pseudoinvert_${ri}$(ap,pinva)

     end function la_pinv_${ri}$_operator

     #:endfor

end module la_pseudoinverse
